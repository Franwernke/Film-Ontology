## Prefixos utilizados ##

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ep1: <http://www.semanticweb.org/cora/ontologies/2021/10/filmes#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

## Consulta 1 ##

SELECT ?filme ?titulo
  WHERE {
    ep1:joel_coen ep1:dirigeEm ?filme.
    ?filme ep1:temTitulo ?titulo.
  } ORDER BY ASC(?titulo)

## Consulta 2 ##

SELECT ?PrimeiroNome ?UltimoNome ?Personagem
  WHERE {
	  ?filme ep1:temTitulo "Anna".
    ?ator ep1:atuaEm ?filme; 
          foaf:firstName ?PrimeiroNome; 
          foaf:familyName ?UltimoNome.
    ?personagem ep1:ehInterpretadoPor ?ator; 
          ep1:ehPersonagemDe ?filme; 
          ep1:temNome ?Personagem.
  } ORDER BY ASC(?ator)

## Consulta 3 ##

SELECT ?Filme ?Diretor ?Ano
  WHERE {
    ?Filme ep1:ehAtuadoPor ep1:scarlett_johansson;
          ep1:ehAtuadoPor ep1:joaquin_phoenix; 
          ep1:ehDirigidoPor ?Diretor; 
          ep1:temAnoDeLancamento ?Ano.
  } ORDER BY DESC(?Ano)

## Consulta 4 ##

SELECT DISTINCT ?Filme ?Duracao
  WHERE {
      ?diretor ep1:dirigeEm ep1:barton_fink;
            ep1:dirigeEm ?Filme.
      { ep1:ted_raimi ep1:atuaEm ?Filme. }
        UNION
      { ep1:john_goodman ep1:atuaEm ?Filme. }
      ?Filme ep1:temDuracao ?Duracao.
  } ORDER BY ASC(?Duracao)

## Consulta 5 ##

SELECT ?Pessoa ?FilmeAt ?FilmeDir
  WHERE {
    ?Pessoa ep1:atuaEm ?FilmeAt;
          ep1:dirigeEm ?FilmeDir.
  }

## Consulta 6 ##

SELECT ?Ano ?Diretor ?Filme
  WHERE {
    ?Filme ep1:temAnoDeLancamento ?Ano; 
          ep1:ehDirigidoPor ?Diretor;
          ep1:ehAtuadoPor ep1:scarlett_johansson;
          ep1:ehAtuadoPor ep1:colleen_camp.
          
    FILTER (2000 <= ?Ano && ?Ano <= 2003)
  } ORDER BY ASC(?Ano)

## Consulta 7 ##

SELECT ?Atriz ?Ator ?Filme ?Duracao
  WHERE {
    ?Atriz ep1:atuaEm ?Filme;
          foaf:gender "feminino"^^xsd:string.
    ?Ator ep1:atuaEm ?Filme;
          foaf:gender "masculino"^^xsd:string.
    
    ?Filme ep1:temDuracao ?Duracao.
    
    FILTER (121 <= ?Duracao && ?Duracao <= 121)
  }

## Consulta 8 ##

SELECT ?Diretor
  WHERE {
    ?filme ep1:ehAtuadoPor ?ator;
          ep1:ehDirigidoPor ?Diretor.
    ?ator foaf:firstName "Scarlett"^^xsd:string;
          foaf:familyName "Johansson"^^xsd:string.
  }
  GROUP BY ?Diretor
  ORDER BY DESC(COUNT(?Diretor))
  LIMIT 1

## Consulta 9 ##

SELECT ?Filme ?Ano ?Personagem
  WHERE {
    ep1:tim_blake_nelson ep1:interpretaEm ?personagemId.
    
    ?personagemId ep1:temNome ?Personagem.
    
    ?Filme ep1:temAnoDeLancamento ?Ano;
          ep1:temPersonagem ?personagemId.
    {
      SELECT (MIN(?Ano) AS ?minimoAnoDeLancamento)
        WHERE {
          ep1:tim_blake_nelson ep1:atuaEm ?Filme.
          ?Filme ep1:temAnoDeLancamento ?Ano.
      } GROUP BY (ep1:tim_blake_nelson)
    }
    FILTER(?Ano = ?minimoAnoDeLancamento)
  }

## Consulta 10 ##

SELECT ?Filme ?Duracao
  WHERE {
    ?Filme ep1:ehDirigidoPor ep1:joel_coen;
          ep1:temDuracao ?Duracao.
    {
      SELECT (MAX(?Duracao) AS ?maxDuracao)
        WHERE {
          ?Filme ep1:ehDirigidoPor ep1:joel_coen;
                ep1:temDuracao ?Duracao.
        } GROUP BY (ep1:joel_coen)
    }
    FILTER(?Duracao = ?maxDuracao)
  }